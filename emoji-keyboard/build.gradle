apply plugin: 'com.android.application'

// map for the version codes
ext.versionCodes = ['mdpi':2, 'hdpi':3, 'xhdpi':4, 'xxhdpi':5].withDefault {1}

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
// assign different version code for each output
    variant.outputs.each { output ->
        output.versionCodeOverride = project.ext.versionCodes.get(output.getFilter(OutputFile.DENSITY)) * 1000000 + android.defaultConfig.versionCode
    }
}


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 13
        targetSdkVersion 29
        versionCode 18
        versionName "Iteration-18"
    }

    splits {
        density {
            enable true
            reset()
            include "mdpi", "hdpi", "xhdpi", "xxhdpi"
            compatibleScreens 'normal', 'large'
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-v4:25.1.1'
    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'
    implementation 'com.orhanobut:logger:1.15'
    implementation 'com.afollestad.material-dialogs:core:0.9.2.3'
    implementation 'com.android.support:recyclerview-v7:25.1.1'
    implementation 'com.android.support:cardview-v7:25.1.1'
}
