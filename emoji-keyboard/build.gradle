buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}
apply plugin: 'com.android.application'

repositories {
        mavenCentral()
}
// map for the version codes
ext.versionCodes = ['mdpi':2, 'hdpi':3, 'xhdpi':4, 'xxhdpi':5].withDefault {1}

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
// assign different version code for each output
    variant.outputs.each { output ->
        output.versionCodeOverride = project.ext.versionCodes.get(output.getFilter(OutputFile.DENSITY)) * 1000000 + android.defaultConfig.versionCode
    }
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 13
        targetSdkVersion 25
        versionCode 17
        versionName "Iteration-17"
    }

    splits {
        density {
            enable true
            reset()
            include "mdpi", "hdpi", "xhdpi", "xxhdpi"
            compatibleScreens 'normal', 'large'
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
}
